plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = '1.20.1-5.0.0'
group = 'com.blakebr0.mysticalcustomization'

base {
    archivesName = 'MysticalCustomization'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

boolean hasCucumber = findProject(':Cucumber') != null
boolean hasMysticalAgriculture = findProject(':MysticalAgriculture') != null
boolean hasMysticalAgradditions = findProject(':MysticalAgradditions') != null

repositories {
    maven { url 'https://maven.blakesmods.com' }
    maven { url 'https://maven.blamejared.com' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

minecraft {
    mappings channel: 'official', version: '1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')

            arg '-mixin.config=cucumber.mixins.json'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            mods {
                mysticalcustomization {
                    source sourceSets.main
                }

                if (hasCucumber) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }

                if (hasMysticalAgriculture) {
                    mysticalagriculture {
                        source project(':MysticalAgriculture').sourceSets.main
                    }
                }

                if (hasMysticalAgradditions) {
                    mysticalagradditions {
                        source project(':MysticalAgradditions').sourceSets.main
                    }
                }
            }
        }

        server {
            workingDirectory project.file('run')

            arg '-mixin.config=cucumber.mixins.json'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            mods {
                mysticalcustomization {
                    source sourceSets.main
                }

                if (hasCucumber) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }

                if (hasMysticalAgriculture) {
                    mysticalagriculture {
                        source project(':MysticalAgriculture').sourceSets.main
                    }
                }

                if (hasMysticalAgradditions) {
                    mysticalagradditions {
                        source project(':MysticalAgradditions').sourceSets.main
                    }
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    if (hasCucumber) {
        implementation(project(':Cucumber')) {
            transitive = false
        }
    } else {
        implementation fg.deobf("com.blakebr0.cucumber:Cucumber:${cucumber_version}")
    }

    if (hasMysticalAgriculture) {
        implementation(project(':MysticalAgriculture')) {
            transitive = false
        }
    } else {
        implementation fg.deobf("com.blakebr0.mysticalagriculture:MysticalAgriculture:${mysticalagriculture_version}")
    }

    if (hasMysticalAgradditions) {
        implementation(project(':MysticalAgradditions')) {
            transitive = false
        }
    } else {
        implementation fg.deobf("com.blakebr0.mysticalagradditions:MysticalAgradditions:${mysticalagradditions_version}")
    }

    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}-forge:${jei_version}")
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'Mystical Customization',
            'Specification-Vendor': 'BlakeBr0',
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': "${version.split('-').last()}",
            'Implementation-Vendor': 'BlakeBr0',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.register('addTestConfigs') {
    project.copy {
        from 'src/test/resources/config'
        into file('run/config')
    }
}

tasks.processResources.finalizedBy('addTestConfigs')
