buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.19.2-4.0.1'
group = 'com.blakebr0.mysticalcustomization'
archivesBaseName = 'MysticalCustomization'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    maven { url 'https://maven.blakesmods.com' }
    maven { url 'https://dvs1.progwml6.com/files/maven/' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

minecraft {
    mappings channel: 'official', version: '1.19.2'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            ideaModule "${rootProject.name}.${project.name}.main"

            mods {
                mysticalcustomization {
                    source sourceSets.main
                }

                if (findProject(':Cucumber') != null) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }

                if (findProject(':MysticalAgriculture') != null) {
                    mysticalagriculture {
                        source project(':MysticalAgriculture').sourceSets.main
                    }
                }

                if (findProject(':MysticalAgradditions') != null) {
                    mysticalagradditions {
                        source project(':MysticalAgradditions').sourceSets.main
                    }
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            ideaModule "${rootProject.name}.${project.name}.main"

            mods {
                mysticalcustomization {
                    source sourceSets.main
                }

                if (findProject(':Cucumber') != null) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }

                if (findProject(':MysticalAgriculture') != null) {
                    mysticalagriculture {
                        source project(':MysticalAgriculture').sourceSets.main
                    }
                }

                if (findProject(':MysticalAgradditions') != null) {
                    mysticalagradditions {
                        source project(':MysticalAgradditions').sourceSets.main
                    }
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    implementation findProject(':Cucumber') ?: fg.deobf("com.blakebr0.cucumber:Cucumber:${cucumber_version}")
    implementation findProject(':MysticalAgriculture') ?: fg.deobf("com.blakebr0.mysticalagriculture:MysticalAgriculture:${mysticalagriculture_version}")
    implementation findProject(':MysticalAgradditions') ?: fg.deobf("com.blakebr0.mysticalagradditions:MysticalAgradditions:${mysticalagradditions_version}")

    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'Mystical Customization',
            'Specification-Vendor': 'BlakeBr0',
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': "${version.split('-').last()}",
            'Implementation-Vendor': 'BlakeBr0',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}